[toc]
# 间接寻址

间接操作符(`@`)允许间接为变量赋值。间接寻址是一种通过数据字段的内容提供部分或全部命令行、命令或命令参数的动态运行时替换的技术。Caché在执行相关命令之前执行替换。

尽管与其他方式相比，间接寻址方式可以更高效更通用的编码，但不是必需的。可以通过`XECUTE`命令达到间接寻址的效果。

只有在明显优势的情况下才应该使用间接寻址。间接寻址可能会影响性能，因为Caché在运行时而不是在编译阶段执行所需的求值。此外，如果使用复杂的间接寻址，应确保清楚的代码逻辑。间接寻址有时很难理解。

**注意：在类中使用标签或例程间接寻址时要指定`Not ProcedureBlock`**

间接寻址由间接运算符(`@`)指定。

```
@variable
```

其中`variable`标识要从中获取替换值的变量。变量可以是数组节点。

```java
ClassMethod Indirection()
{
	s x = "A"
	s x(1) = "B"
	d @x(1)
	q
A(var)
	w !,"A"
	q
B(var)
	w !,"B"
	q
}
```

Caché识别五种类型的间接寻址：

- Name indirection
- Pattern indirection
- Argument indirection
- Subscript indirection
- $TEXT argument indirection

执行哪种类型的间接寻址取决于`@variable`出现的上下文。下面将分别描述每种类型的间接连接。

**间接寻址符不能与点语法一起使用。这是因为点语法是在编译时解析的，而不是在运行时解析的。**

## 名称间接寻址

在名称间接寻址中，间接计算结果为变量名、行标签或例程名称。在执行该命令之前将变量的内容替换为预期的名称。

名称间接寻址只能访问公共变量。

使用间接地址引用命名变量时，间接地址的值必须是完整的全局变量或局部变量名，包括任何必要的下标。

```java
ClassMethod Indirection1()
{
	s Y = "B"
	w Y,!
	s @Y = 6
	if Y '= @Y w "不相等！",!
	w @Y,!
}
```

**重要提示：如果使用间接方式获取或设置对象属性值，可能会导致错误。不要使用此类调用，因为会绕过属性访问器方法(<PropertyName>Get和<PropertyName>Set)。相反，可以使用$CLASSMETHOD、$METHOD和$PROPERTY)函数。**

- 当使用间接寻址引用行标签时，间接的值必须是语法上有效的行标签。
  - 如果`name`的值为`1`，则标签的值为`apple`。
  - 如果`name`的值为`2`，则标签的值为`banana`。
  - 如果`name`的值为其他时，则标签的值为`stop`。

```java
ClassMethod Indirection2(name)
{
	s fruit = $select(name = 1 : "apple", name = 2 : "banana", 1 : "stop")
fruit 
	goto @fruit
	q
apple
	w "apple"
	q
banana
	w "banana"
	q
stop
	w "stop"
	q
}
```

- 当使用间接引用例程名时，间接寻址的值必须是语法上有效的例程名。

```java
ClassMethod Indirection3(name)
{
	s fruit = $select(name = 1 : "apple1", name = 2 : "banana1", 1 : "stop1")
	d @fruit
	q
apple1()
	w "apple1"
	
banana1()
	w "banana1"
	
stop1()
	w "stop1"
}
```

- 名称间接寻址只能替换名称值。

```java
ClassMethod Indirection4(name)
{
	s var1 = "var2"
	s var2 = 5
	s x = @var1 * 6
	w x,!
}

```

- 当计算等号右边的表达式时将`@var1`解释为对变量名的间接引用，而不是数值。
```java
 	s var1 = "5"
 	s x = @var1 * 6
	w x,!
```

## 匹配模式间接寻址

匹配模式间接寻址是间接寻址的一种特殊形式。间接运算符替换模式匹配。间接的值必须是有效的模式。

- 间接寻址与模式匹配一起使用，以检查有效的15位身份证或18位身份证。

```java
ClassMethod Indirection5(id)
{
	s pattern = "18N"
	s:($length(id) = 15) pattern = "15N"
	if id ? @pattern { 
		w !,"有效身份证"
	}
}
```

在模式匹配中使用间接寻址是定位应用程序中使用的模式匹配的一种便捷方式。在这种情况下，可以将模式存储在单独的变量中，然后在实际的模式匹配测试中间接引用它们。只需要修改模式匹配变量本身。

## 参数间接寻址

在参数间接寻址中，间接寻址计算一个或多个命令参数。

```java
ClassMethod Indirection6()
{
	s a = "var1",b = "var2 = 3 * 4 + var1"
	s @a = 5*6
	s @b
	w "a = ",a,!
	w "b = ",b,!
	w @a,!
	s var2 = 3
	s var1 = 6
	w @b,!
}
```

## 下标间接寻址

下标间接寻址是名字间接寻址的扩展形式。在下标间接寻址中，间接寻址的值必须是本地或全局数组节点的名称。下标间接在语法上不同于其他形式的间接。下标间接使用以下格式的两个间接运算符:

```
@array@(subscript) 
```

```java
ClassMethod Indirection7()
{
	k ^color
	s ^color(1) = "red"
	s ^color(2) = "yellow"
	s ^color(3) = "black"
	s ^color(4) = "green"
	s ^color(5) = "write"
	
	s ^color(1, 1) = "red1"
	s ^color(1, 2) = "red2"
	s ^color(1, 3) = "red3"
	s ^color(1, 4) = "red4"
	s ^color(1, 5) = "red5"
	
	zw ^color
	
	s global = "^color"
	s node = "1"
	s sub = "1"
	
	w @global@(node, sub),!
	//w @global(node, sub),!
	
	s ID = ""
	for {
		s ID = $o(^color(ID))
		q:(ID = "")
		s data = $zr
		w @data,!
		
		s sub = ""
		for {
			s sub = $o(^color(ID , sub))
			q:(sub = "")
			w "Indirection:" _ @global@(ID, sub),!

		}
	}
}
```
- 自定义全局变量名称，节点名称使用下标间接寻址输出。

```java
/// Sample.PersonD
ClassMethod Indirection8()
{
	r !,"global bame: ", name
	r !,"start: ", start
	r !,"end: ", end

	s array = "^" _ name
	s subNode = "Employee"
	for i = start : 1:end {
		if ($d(@array@(i))) {
			//w "@array@("_ i _")：" _ @array@(i),!
			w "@array@("_ i _")：" _ $li(@array@(i), 3),!
		}
		if ($d(@array@(i, subNode))){
			//w "@array@("_ i _ "," _ subNode _ ")：" _ @array@(i, subNode),!
			w "@array@("_ i _")：" _ $li(@array@(i, subNode), 1),!
		}
	}
	q
}
```

## $TEXT参数间接寻址

`$TEXT`参数间接只允许在`$TEXT`函数参数的上下文中使用。间接地址的值必须是有效的`$TEXT`参数。

```java
ClassMethod Indirection9()
{
	s info = "text"
	s ret = $text(@info)
	d @ret
	q 
text
	;23232
	b
	w "AA",!
	q
}
```