[toc]

# 模式匹配

Caché支持两种模式匹配系统：
- Caché模式匹配是一种用问号(`?`)分隔模式字符串开头的语法。或(`'?`)。
- 正则表达式可以与`$LOCATE`和`$MATCH`函数以及`%Regex.Matcher`类的方法一起使用。**(这些函数方法在2016之后才有，2010版本没有。)**

**这些模式匹配系统是完全独立的。每个模式匹配系统只能在其自己的上下文中使用。但是，可以使用逻辑AND和OR语法组合来自不同模式匹配系统的模式匹配测试。**

```java
	s var = "abcDEf"
	if (var ?.e2U.e) && $match(var, "^.{3,7}") { 
		w "match!"
	} else { 
		w "no match"
	}
```


- 模式匹配运算符测试其左操作数中的字符是否由右操作数中的模式正确指定。它返回一个布尔值。当模式正确指定左操作数中的字符模式时，模式匹配运算符会产生`TRUE(1)`结果。如果模式没有正确指定左操作数中的字符模式，则生成`FALSE(0)`结果。

```java
	s str = "521-13-1411"
 	s match = str ? 3N1"-"2N1"-"4N
 	w match,!
```


## 提供的特殊模式代码

| 代码       | 含义                                                         |
| ---------- | ------------------------------------------------------------ |
| `A`        | 匹配任何大写或小写字母字符。`包括ASCII值65到90(A到Z)、97到122(a到z)、170、181、186、192到214、216到246和248到255`。 |
| `C`        | 匹配任何ASCII控制字符`(ASCII值0到31和扩展ASCII值127到159)`。 |
| `E`        | 匹配任何字符，包括非打印字符、空格字符和控制字符。           |
| `L`        | 匹配任何小写字母字符。`包括ASCII值97到122(a到z)、170、181、186、223到246和248到255`。 |
| `N`        | 匹配10个数字字符0到9`(ASCII 48到57)`中的任何一个。           |
| `P`        | 匹配任何标点符号。`包括ASCII值32到47、58到64、91到96、123到126、160到169、171到177、180、182到184、187、191、215和247`。 |
| `U`        | 匹配任何大写字母字符。`包括ASCII值65到90(A到Z)、192到214和216到222`。 |
| `R,B,M`    | 匹配西里尔8位字母字符映射。R匹配任何西里尔字符`(ASCII值192到255)`。B匹配大写西里尔字符`(ASCII值192到223)`。M匹配小写西里尔字符`(ASCII值为224到255)`。这些模式代码仅在俄语8位`Windows`区域设置`(Ruw8`)中有意义。在其他地区，可以成功执行，但无法匹配任何字符。 |
| `ZFWCHARZ` | 匹配日语`ZENKAKU`字符集中的任何字符。`ZFWCHARZ`匹配全角字符，例如汉字范围中的字符，以及在某些终端仿真器显示时占用双倍单元格的许多非汉字字符。`ZFWCHARZ`还匹配`JIS2004`标准中定义的`303`个代理项对字符，将每个代理项对视为单个字符。例如，代理项对字符`$WC(131083)`与`？1ZFWCHARZ`匹配。此模式匹配代码需要日语区域设置。 |
| `ZHWKATAZ` | 匹配日文汉字假名字符集中的任何字符。这些是`Unicode`值`65377(FF61)`到`65439(FF9F)`。此模式匹配代码需要日语区域设置。 |

```java
	s var = "A"
	w "字母字符? "
	w var ? 1A,!
	
	s var = $char(10)
	w "控制字符? "
	w var ? 1C,!
	
	s var = $char(13) _ "A" _ "a"
	w "任何字符? "
	w var ? 3E,!
	
	s var = "a"
	w "小写字母? "
	w var ? 1L,!
	
	s var = "2"
	w "数字字符? "
	w var ? 1N,!
	
	s var = "."
	w "标点符号? "
	w var ? 1P,!
	
	s var = "U"
	w "大写字母? "
	w var ? 1U,!
```

 - 特殊模式代码不区分大小写；可以用大写或小写指定。

```java
	s str = "12345"
 	s match = str ? 5N
 	w match,!
 	
 	s match = str ? 5n
 	w match,!
 	
	s str = "1"
 	s match = str ? 1NU
 	w match,!
 	
 	s str = "A"
 	s match = str ? 1nu
 	w match,!
```

## 指定模式可以出现的次数

- 要定义模式可在目标操作数中出现的次数范围，请使用以下格式：

```
    n.n
```

```java
	s str = "NNNNN"
	s match = str ? 1.5"N"
	w match,!
	
	s str = "NNN"
	s match = str ? 1.5"N"
	w match,!
	
	s str = "nbnbnbnb"
	s match = str ? 1.5"nb"
	w match,!
```

- 匹配出现`N`次特殊模式代码

```java
	s str = "Io"
 	s match = str ? 1.2A
 	w match,!
```

- 省略前导第一个`n`的默认值为零。省略后导第二个`n`的默认值是任何数字。都省略匹配任意数量字符。

```java
	s str = ""
 	s match = str ? .2L
 	w match,!
 	
	s str = "ABCDEFG"
 	s match = str ? 1.U
 	w match,!
        
	s str = "aaaaaaaaaaa"
	s match = str ? .A
	w match,!
```

## 指定多个模式

- 要定义多个模式，可以将`n`和模式组合成任意长度的序列。

```java
	s str = "2021-07-11"
 	s match = str ? 4N1"-"2N1"-"2N
 	w match,!
 	
	s str = "7/11/21"
 	s match = str ? 1.2N1"/"1.2N1"/"1.4N
 	w match,!
 	
	s str = "12/1/2021"
 	s match = str ? 1.2N1"/"1.2N1"/"1.4N
 	w match,!
```

## 指定组合模式

```
Pattern1Pattern2
```
- 使用组合模式时，将对照目标操作数检查由`pattern1`后跟`pattern2`组成的序列。

```java
	s str = "123"
 	s match = str ? 3N.4L
 	w match,!
 	
	s str = "123abcde"
 	s match = str ? 3N.4L
 	w match,!
```

## 指定交替模式

- 测试操作数是否与一组指定模式序列中的一个或多个匹配。它为模式匹配提供了逻辑或功能。

```
( pattern-element sequence {, pattern-element sequence }...)
```
- 如果`str`包含字母`“A”`的一个匹配项或字母`“B”`的一个匹配项，则以下模式返回`TRUE(1)`。
```java
	s str = "A"
 	s match = str ? 1(1"A",1"B")
 	w match,!
 	
	s str = "B"
 	s match = str ? 1(1"A",1"B")
 	w match,!
 	
	s str = "AB"
 	s match = str ? 1(1"A",1"B")
 	w match,!
 	
```

- 重复计数大于一的交替可以产生许多可接受模式的组合。

```java
 	s str = "YX"
 	s match = str ? 2(1"Y",1"X")
 	w match,!
 	
 	s str = "YY"
 	s match = str ? 2(1"Y",1"X")
 	w match,!
 	
 	s str = "XXY"
 	s match = str ? 2(1"Y",2"X")
 	w match,!
 	
 	s str = "XXXX"
 	s match = str ? 2(1"Y",2"X")
 	w match,!
```

- 匹配15位与18位身份证号

```java
 	s str = "123456789012345"
 	s match = str ? 15N
 	w match,!
 	
 	s str = "12345678901234567X"
 	s match = str ? 17N1"X"
 	w match,!
 	
 	s str = "123456789012345"
 	s match = str ? 1(17N1"X",15N)
 	w match,!
 	
 	s str = "12345678901234567X"
 	s match = str ? 15N1(2N1"X",0N)
 	w match,!
```

## 使用不完整的模式

- 如果模式匹配成功地仅匹配了字符串的一部分，则模式匹配将返回`FALSE(0)`结果。也就是说，当模式完成时不能有任何剩余的字符串。

```java
	s str = "RAW BAR"
 	s match = str ? .U1P2U
 	w match,!
 	
	s str = "RAW BAR"
 	s match = str ? .U1P3U
 	w match,!
 	
	s str = "RAW BAR"
 	s match = str ? .U1P4U
 	w match,!
```

- 当模式与操作数匹配时，可以有多个模式解释。

```java
	s str = "/A#####B$$$$$"
 	s match = str ? .E1U.E
 	w match,!
```

1. 第一个`“.E”`匹配子字符串`“/”`，`1U`匹配`“A”`，第二个`“.E”`匹配子字符串`“#####B$$$$$”`。
2. 第一个`“.E”`匹配子字符串`“/A#####”`，`1U`匹配字符`“B”`，第二个`“.E”`匹配子字符串`“$$$$$”`。

## 不匹配运算符

- 可以通过将一元`NOT`运算符(`'`)与模式匹配一起使用来生成不匹配操作：

```java
	w !,"abc" ? 3L
	w !,"abc" '? 3L
	w !,"abc" ? 3N
	w !,"abc" '? 3N
	w !,"abc" '? 3E
```

## 模式复杂性

**注意：具有多个交替和不确定模式的模式匹配在应用于长字符串时，会递归到系统堆栈中的多个级别。在极少数情况下，这种递归可能会上升到几千个级别，从而威胁到堆栈溢出和进程崩溃。当这种极端情况发生时，Caché会发出`<Complex Pattern>`错误，而不是冒当前进程崩溃的风险。**

**如果出现此类错误，建议简化模式，负责的模式则不应该使用模式匹配。**