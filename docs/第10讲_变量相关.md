[toc]

# 第10讲 变量

- 通常，可以使用`set`命令通过为变量赋值来定义变量。可以将空字符串(`""`)值赋给变量。大多数命令和函数在使用之前都需要定义变量。如果变量未定义，则默认情况下，引用它会生成`<UNDEFINED>`错误。

```java
	s yx = ""
	s yx = b
```

```java
 
 s yx = b }
 ^
<UNDEFINED>zVariable+2^M.Ten.1 *b
```

- 通过设置`%SYSTEM.Process.Unfined()`方法，可以使其在引用未定义变量时不会生成`<UNDEFINED>`错误。
  - `0` 默认为`0`，对任何未定义的变量发出`<UNDEFINED>`错误。
  - `1` 对于任何未定义的变量返回一个空字符串。

```java
	d ##class(%SYSTEM.Process).Undefined(2)
	s yx = ""
	s yx = b
```

- 可以在一些操作中使用未定义的变量。

```java
	s yx = $increment(c)	//自增
	s yx = $get(d)			//保护如果未定义取返回""
	s yx = $data(e)			//判断变量是否存在
```

# 局部变量

- 局部变量是存储在当前Caché进程中的变量。只有创建它的进程才能访问它。它被映射为可以从所有命名空间访问。当进程结束时，所有进程的局部变量都会被删除。
- 当在事务里时不会将局部变量`SET`或`KILL`视为日志记录的事务事件；回滚事务对这些操作没有任何影响。

```java
	s a = 2
	ts	
	s a = 1
	tro
	w a,!
```
# 百分比变量

- 局部变量名必须是有效的标识符。其第一个字符必须是字母或百分号(`%`)字符。以“`%`”字符开头的变量名称称为“百分比变量”，具有不同的作用域规则。只有以“`%Z`”或“`%z`”开头的变量才可用于应用程序代码；所有其他百分比变量将保留给系统使用。百分号(`%`)字符只能用作局部变量名称的第一个字符。局部变量名的其他字符可以是字母或数字。

```java
/// d ##class(YX.Third).Variable1()
ClassMethod Variable1()
{
	s %za = "百分比变量"
	w %za,!
}

/// d ##class(YX.Third).Variable2()
ClassMethod Variable2()
{
	w %za,!
}
```
```java
DHC-APP>d ##class(YX.Third).Variable1()
百分比变量
 
DHC-APP>d ##class(YX.Third).Variable2()
百分比变量
```
- 通常复用一些固定配置的时候会用到它。

```
	q:($d(%zconfig))
	s %zconfig = ##class(XX).XX()
```

- 局部变量名称限制为`31`个字符。可以指定长度超过`31`个字符的名称，但仅使用前`31`个字符。因此，局部变量名在其前`31`个字符内必须是唯一的。

```java
	s abcdef1234abcdef1234abcdef1234a = 1
	s abcdef1234abcdef1234abcdef1234ab = 2
	w abcdef1234abcdef1234abcdef1234a,!
```
```java
2
```

# ProcedureBlock使用

- 私有变量。程序块中使用的任何变量都自动成为私有变量，并且仅在该程序块中可见。默认情况下，创建的所有对象方法都使用过程块(`ProcedureBlock`类关键字在类定义中设置)，因此，默认情况下，在方法中创建的所有变量都是私有变量。不能对私有变量使用`new`命令。

```java
Class YX.Third Extends %RegisteredObject [ Not ProcedureBlock ]
    /// d ##class(YX.Third).ProcedureBlock()
    ClassMethod ProcedureBlock()
    {
    	for i = 1 : 1: 5{
    		d ..ProcedureBlock1()
    	}
    }
    
    ClassMethod ProcedureBlock1()
    {
    	w "这是Not ProcedureBlock",!
    	s i = 2
    }
}
```
运行发现是一个死循环，!

- 如果在 `Not ProcedureBlock` 类里传参数一定要初始化`n`，否则会未定意。

```java
/// d ##class(YX.Third).ProcedureBlock()
ClassMethod ProcedureBlock()
{
	for i = 1 : 1: 5{
		d ..ProcedureBlock1("this is a String")
	}
}

ClassMethod ProcedureBlock1(var)
{
	n (var)
	w var,!
	w "这是Not ProcedureBlock",!
	s i = 2
}
```
# 进程私有全局变量

- 进程私有全局变量是一个只能由创建它的进程访问的变量。它被映射为可以从所有命名空间访问。当该进程结束时，其所有进程私有全局变量都将被删除。

```java
	s ^||yx = 2
	zw ^||yx
	s $namespace = "%sys"
	zw ^||yx
```

- 进程私有全局变量旨在用于大量数据时。
- 进程专用全局名称采用以下形式之一,第一种形式(`^||name`)最常见，也是新代码推荐使用的形式。提供第二、第三和第四种形式是为了与定义全局变量的现有代码兼容。它们允许指定一个变量，该变量确定是将名称定义为进程私有全局还是标准全局变量。
```java
    ^||name 
    ^|"^"|name 
    ^["^"]name 
    ^["^",""]name
```

- 回滚事务对进程私有全局变量这些操作没有任何影响。

```java
/// d ##class(YX.Third).Variable5()
ClassMethod Variable5()
{
	k ^||yx("yaoxin")
	s ^||yx("yaoxin") = "姚鑫"
	ts	
	s ^||yx("yaoxin") = "tro"
	tro
	w ^||yx("yaoxin")
}
```

```java
DHC-APP>d ##class(YX.Third).Variable5()
tro
```
- 进程专用全局名称限制为`31`个字符，不包括前缀字符。可以指定长度超过`31`个字符的名称，但仅使用前`31`个字符。因此，进程专用全局名称在其前`31`个字符内必须是唯一的。

```java
	s ^||abcdef1234abcdef1234abcdef1234a = 1
	s ^||abcdef1234abcdef1234abcdef1234ab = 2
	zw ^||abcdef1234abcdef1234abcdef1234a
	zw ^||abcdef1234abcdef1234abcdef1234ab
```
```java
^||abcdef1234abcdef1234abcdef1234a=2
^||abcdef1234abcdef1234abcdef1234a=2
```

# 使用^GETPPGINFO

- 使用`^GETPPGINFO`工具以块为单位显示当前进程私有全局变量及其空间分配`^GETPPGINFO`列出进程私有全局变量的下标或值。可以通过指定进程`ID(PID)`来显示特定进程的进程私有全局变量，也可以通过指定`“*”`通配符字符串来显示所有进程的进程私有全局变量。
- 必须在`%SYS`命名空间中才能调用`^GETPPGINFO`。

```java
	s ^||fruit("apple","red") = "apple red"
	s ^||fruit("banana","yellow") = "banana yellow"
	s ^||fruit("pear","blue") = "pear blue"
	s ^||person("china","悟空")="china 悟空"
	s ^||person("usa","美国队长")="usa 美国队长"
	s $namespace = "%SYS"
	d ^GETPPGINFO("*")
	s $namespace = "dhc-app"
```

- 指定进程ID显示信息。

```java
Process ID: 28668 --> Total PPG Block count 7, block size 8,192
     PPG: fruit uses 2 blocks
     PPG: person uses 2 blocks
```

- 将进程globe导出

```java
%SYS>d ^GETPPGINFO("*","T","E:/temp/ppg.txt")
%SYS>d ^GETPPGINFO("*","MNN","E:/temp/ppg.txt")
```


# 全局变量

- 全局变量是一种特殊类型的变量，它自动存储在Caché数据库中。它被映射到特定的命名空间，并且只能在该命名空间内访问，除非使用扩展引用。全局可以由任何进程访问。全局在创建它的进程终止后仍然存在。它会一直存在，直到被明确删除。

```java
 	s mylocal = "这是局部变量"
 	s ^myglobal = "这是存储在当前命名空间中的全局变量"
```

```java
%SYS>zw ^|"dhc-app"|myglobal
^|"dhc-app"|myglobal="这是存储在当前命名空间中的全局变量"
```

- 将全局的设置或终止视为日志记录的事务事件；回滚事务将撤消这些操作。


```java
	k ^myglobal("a")
	zw ^myglobal("a")
	ts
	s ^myglobal("a") = "tro"
	tro
	zw ^myglobal("a")
```

- 全局名称限制为`31`个字符，不包括前缀字符。可以指定长度超过`31`个字符的名称，但仅使用前`31`个字符。因此，全局名称的前`31`个字符必须是唯一的。

```java
	s ^abcdef1234abcdef1234abcdef1234a = 1
	s ^abcdef1234abcdef1234abcdef1234ab = 2
	zw ^abcdef1234abcdef1234abcdef1234ab
```
- 可以使用`$ZREFERENCE`特殊变量来确定最近使用的全局变量的名称。可以使用`$QSUBSCRIPT`函数返回指定全局变量节点的名称，或使用`$QLENGTH`函数返回下标的数量。
```java
	s ^color(1) = "red"
	s ^color(2) = "yellow"
	s ^color(3) = "write"
	s ^color(4) = "black"
	s ^color(5) = "green"

	s ID = ""
	for {
		s ID = $o(^color(ID))
		q:(ID = "")
		s data = $zr
		w "$ZREFERENCE:" _ @data,!
		w "$QSUBSCRIPT:" _ $qs($zr,0),!
		w "$QLENGTH:" _ $ql($zr),!
		
	}
```

- 对于当前进程，局部变量名必须是唯一的。其他进程可能具有同名的局部变量。进程全局变量或全局变量可以与局部变量，百分比变量同名。例如：`yx`、`^||yx`、`^yx`、`%yx`是个不同的变量。

```java
	k ^yx, ^||yx
	s yx = 1
	w yx,!
	s ^||yx = 2
	zw ^||yx
	s ^yx = 3
	zw ^yx
	s %yx =4
	w %yx,!
```

```java
1
^||yx=2
^yx=3
4
```
- 推荐变量使用顺序局部变量 -> 进程全局变量 -> 全局变量。百分比变量看情况使用。

# 下标变量与数组变量

- 局部变量、进程私有变量和全局变量都可以采用下标。
- 下标的最大长度为`511`个编码字节。超过最大下标长度会导致错误。但是，允许的最长整数是`309`位；超过此限制将导致`<MAXNUMBER>`错误。因此，必须将长度超过`309`个字符的数字下标指定为字符串。
- 局部变量的最大下标级别数为`255`。全局变量或进程私有全局变量的最大下标级别数为`253`。超过最大下标级别数会导致错误。
- 下标为数字的为数组变量。下标变量包含数组变量。
# 变量类型和转换

from  | to| 规则
---|---|---
Number | String| 使用表示数字值的字符串，例如上例中变量`num`的`2.2`。
String | Number| 字符串的前导字符被解释为数字文字，例如，“`-1.20abc`”被解释为`-1.2`，而“`abc123`”被解释为`0`。
Object | Number| 使用给定对象引用的内部对象实例编号。该值为整数。
Object | String|使用`n@cls`形式的字符串，其中`n`是内部对象实例编号，`cls`是给定对象的类名。
Number | Object| 不允许
String | Object| 不允许

# 使用#Dim定义变量类型

- 在Studio中编写代码时，可以使用`#Dim`预处理器指令。`#Dim`提供有关所需变量类型的信息。Studio使用此信息通过Studio Assist功能完成代码。

```java
	#Dim President As %String = "yaoxin"
	w President,!
```

